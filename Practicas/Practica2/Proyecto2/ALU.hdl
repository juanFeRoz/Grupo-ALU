// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zero the x
    Mux16(a=x,b=false,sel=zx,out=x1);
    // negate x
    Not16(in=x1,out=nx1);
    Mux16(a=x1,b=nx1,sel=nx,out=xf1);
    // zero the y
    Mux16(a=y,b=false,sel=zy,out=y1);
    // negate y
    Not16(in=y1,out=ny1);
    Mux16(a=y1,b=ny1,sel=ny,out=yf1);
    // AND or sum
    Add16(a=xf1,b=yf1,out=abADD);
    And16(a=xf1,b=yf1,out=abAND);
    Mux16(a=abAND,b=abADD,sel=f,out=out1);
    //negate the ouput ?
    Not16(in=out1,out=Nout1);
    Mux16(a=out1,b=Nout1,sel=no,out=out,out=outn,out[0..7]=outn1,out[8..15]=outn2,out[15]=outng);
    // check if the output is 0
    Or8Way(in=outn1,out=zr1);
    Or8Way(in=outn2,out=zr2);
    Or(a=zr1,b=zr2,out=zrInt);
    Not(in=zrInt,out=zr);
    // check if the number<0
    Or(a=outng,b=false,out=ng);
}